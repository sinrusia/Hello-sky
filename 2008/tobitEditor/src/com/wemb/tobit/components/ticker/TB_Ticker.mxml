<?xml version="1.0" encoding="utf-8"?>
<mx:HBox xmlns:mx="http://www.adobe.com/2006/mxml" 
	creationComplete="initComp()"
	verticalAlign="middle"
	xmlns:mxeffects="com.adobe.ac.mxeffects.*"
	horizontalGap="5" paddingBottom="3"
	>
	
	<!--
	backgroundColor="#FFFFF"
	backgroundAlpha="0"
	rollOver="onRollOver(event)"
	rollOut="onRollOut(event)">
	-->
	
	<mx:Script>
		<![CDATA[
			import mx.effects.Move;
			import mx.collections.ArrayCollection;
			import mx.events.EffectEvent;

			private var _textArr:ArrayCollection;
			
			[Bindable]
			public var index:int=0;
			
			//Timer
			private var timer:Timer;
			
			//Mouse Over 상태를 표시한다.
			private var isOver:Boolean = false;
			
			private function initComp():void
			{
				_textArr = new ArrayCollection();
				timer = new Timer(6000);
				timer.addEventListener(TimerEvent.TIMER, tickerTimerEvent);
			}
			
			public function set textArr(arr:ArrayCollection):void
			{
				_textArr = arr;
				index = 0;
				displayMessage();
			}
			
			private function displayMessage():void
			{
				//타이머 멈춤
				timer.stop();
				
				//위치 초기화한다.
				tickerText.x = 0;
				
				if( _textArr.length > 0 )
				{
					var currentIndex:Number = index % _textArr.length;
					
					tickerText.text = _textArr[currentIndex].text;
					tickerText.validateNow();
					
					if(tickerText.textWidth > tickerBox.width)
					{ 
						if(mvEff.isPlaying)
							mvEff.end();
						
						mvEff.target = tickerText;
						mvEff.xTo = (tickerBox.width - tickerText.textWidth);
						mvEff.duration = Math.round(tickerText.textWidth * 4.5);
						
						timer.delay = mvEff.duration + 6000;
						
						mvEff.play();
					}
					
					//타이머 재시작
					timer.start();
					
					// index 카운팅
					index++;
					
				}else{
					tickerText.text = "등록된 메세지가 없습니다.";
				}
			}
			
			private function tickerTimerEvent(e:TimerEvent):void
			{
				timer.stop();
				tickerChange.play();
			}
			
			private function hideHandler(e:EffectEvent):void
			{
				if(!isOver){
					displayMessage();
				}
			}
			
			private function onRollOver(event:MouseEvent):void
			{
				isOver = true;
				tickerChange.reverse();
				timer.stop();
			}
			
			private function onRollOut(event:MouseEvent):void
			{
				isOver = false;
				timer.start();
			}
			
		]]>
	</mx:Script>
	<mx:Sequence id="tickerChange">
		<mx:Fade id="hideTicker" alphaFrom="1" alphaTo="0" duration="600" target="{tickerText}" effectEnd="hideHandler(event)"/>		
		<mx:Fade id="showTicker" alphaFrom="0" alphaTo="1" duration="600" target="{tickerText}"/>
	</mx:Sequence>
	
	<mx:Move id="mvEff" duration="1000"  />
	
	<mx:Image source="assets/common/b02.png"/>
	
	<mx:Canvas id="tickerBox" width="100%" verticalScrollPolicy="off" horizontalScrollPolicy="off"> 
		
		<mx:Label id="tickerText" text="등록된 메세지가 없습니다." color="white" fontWeight="bold"
			buttonMode="true" mouseChildren="false" useHandCursor="true" />
	</mx:Canvas>
</mx:HBox>
